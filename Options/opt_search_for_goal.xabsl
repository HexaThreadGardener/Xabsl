/*
 * opt_search_for_goal: Search for the goal, by iterating over the "far" head directions.
 *
 * Reads:
 * - isGoalSeen
 *
 * Writes:
 * - headMode
 * - goal_found_headMode
 *
 * Behaves:
 * - behavior_nothing
 * - behavior_initialize
 */

option opt_search_for_goal
{
    initial state start_goal_search                 // 0
    {
        decision {
            goto look_up_left;
        }
        action
        {
            behavior_nothing();
        }
    }

    state look_up_left          // 1
    {
        decision
        {
            if (isGoalSeen)
            {
                goto goal_seen;
            }
            else if (state_time < T_SEARCH_BALL)
            {
                stay;
            }
            else
            {
                goto look_up_mid;
            }
        }
        action
        {
            headMode = FarLeft;
            behavior_initialize();
        }
    }

    state look_up_mid           // 2
    {
        decision
        {
            if  (isGoalSeen)
            {
                goto goal_seen;
            }
            else if (state_time < T_SEARCH_BALL)
            {
                stay;
            }
            else
            {
                goto look_up_right;
            }
        }
        action
        {
            headMode = FarMid;
            behavior_initialize();
        }
    }

    state look_up_right         // 3
    {
        decision
        {
            {
                if (isGoalSeen)
                {
                    goto goal_seen;
                }
                else if (state_time < T_SEARCH_BALL)
                {
                    stay;
                }
                else
                {
                    goto look_up_left;
                }
            }
        }
        action
        {
            headMode = FarRight;
            behavior_initialize();
        }
    }


    target state goal_seen // 13
    {
        decision
        {
            stay;
        }
        action
        {
            goal_found_headMode = headMode;
            behavior_nothing();
        }
    }
}

